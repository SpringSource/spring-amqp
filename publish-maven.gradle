apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}

		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = 'https://github.com/SpringSource/spring-amqp'
			organization {
				name = 'SpringSource'
				url = 'http://springsource.org'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = 'https://github.com/SpringSource/spring-amqp'
				connection = 'scm:git:git://github.com/SpringSource/spring-amqp'
				developerConnection = 'scm:git:git://github.com/SpringSource/spring-amqp'
			}

			developers {
				developer {
					id = 'dsyer'
					name = 'Dave Syer'
					email = 'dsyer@vmware.com'
				}
				developer {
					id = 'markfisher'
					name = 'Mark Fisher'
					email = 'markfisher@vmware.com'
				}
				developer {
					id = 'mpollack'
					name = 'Mark Pollack'
					email = 'mpollack@vmware.com'
				}
				developer {
					id = 'jbrisbin'
					name = 'Jon Brisbin'
					email = 'jbrisbin@vmware.com'
				}
				developer {
					id = 'tomas.lukosius'
					name = 'Tomas Lukosius'
					email = 'tomas.lukosius@opencredo.com'
				}
			}
		}
	}
}